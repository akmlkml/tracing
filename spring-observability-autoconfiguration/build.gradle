import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.1.5'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'uz.b2c.kapitalbank.observability'
version = '0.0.1'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-autoconfigure-processor'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    implementation 'org.springframework.boot:spring-boot'
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'org.apache.logging.log4j:log4j-core:2.20.0'

    // Actuator endpoints allow you to monitor and interact with your application.
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    // Bill of Materials (BOM) which is a project that contains all the project versions for you
    implementation platform('io.micrometer:micrometer-tracing-bom:1.1.6')
    // tracer
    implementation 'io.micrometer:micrometer-tracing-bridge-brave:1.1.6'
}

tasks.register('sourceJar', Jar) {
    from sourceSets.main.allSource
    archiveClassifier.set("sources")
}

publishing {
    publications {
        autoconfiguration(MavenPublication) {
            from components.java
            artifact sourceJar
        }
    }
    repositories {
        mavenLocal()
        maven {
            name 'nexus'
            url "https://nexus.paymart.uz/repository/maven-releases/"
            credentials {
                username System.getenv("GRADLE_USER")
                password System.getenv("GRADLE_PASSWORD")
            }
        }

    }
}